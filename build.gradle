buildscript {
    repositories {
        // Shadow
        jcenter()
        // Blossom & Gradle Versions
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath 'gradle.plugin.net.kyori:blossom:1.1.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
    }
}

defaultTasks 'clean', 'build', 'publish'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.johnrengelman.shadow' // Shadow jar
    apply plugin: 'net.kyori.blossom' // Token replacer
    apply plugin: 'com.github.ben-manes.versions' // Dependencies version checker


    group 'com.alessiodp.core'
    version '1.1.2'

    sourceCompatibility = 1.8

    // Development
    ext.lombok = '1.18.8'
    ext.powermock = '2.0.2'

    // Libraries
    ext.bstats = '1.5'
    ext.configurate = '3.6.1'
    ext.gson = '2.8.4'
    ext.guava = '21.0'
    ext.hikaricp = '3.3.1'
    ext.slf4j = '1.7.25'
    ext.spigot = '1.14.3-R0.1-SNAPSHOT'
    ext.bungeecord = '1.14-SNAPSHOT'
    ext.sqlitejdbc = '3.27.2.1'

    repositories {
        mavenCentral()
        
        // Spigot repository for Spigot & Bukkit
        maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        // OSS Sonatype repository for Bungeecord
        maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
        // JitPack repo
        maven { url = 'https://jitpack.io' }
        // Sponge repository for configurate
        maven { url = 'https://repo.spongepowered.org/maven' }
    }

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: project.ext.guava

        compile group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: project.ext.lombok

        testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: project.ext.powermock
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: project.ext.powermock
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'SNAPSHOT'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }

                boolean snapshots = ['spigot-api', 'bukkit'].any { name ->
                    selection.candidate.module == name
                }
                if ((selection.candidate.module == 'guava' && selection.candidate.version != project.ext.guava)
                        || (selection.candidate.module == 'gson' && selection.candidate.version != project.ext.gson)) {
                    selection.reject('Module unsupported')
                }

                if (rejected && !snapshots) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    jar {
        enabled = false
        dependsOn shadowJar
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
        archiveClassifier = 'sources'
    }

    task javadocJar(type: Jar) {
        from javadoc
        archiveClassifier = 'javadoc'
    }

    artifacts {
        archives javadocJar
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                pom {
                    url = 'https://alessiodp.com/'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'AlessioDP'
                            email = 'me@alessiodp.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/AlessioDP/ADP-Core.git'
                        developerConnection = 'scm:git:git@github.com:AlessioDP/ADP-Core.git'
                        url = 'http://github.com/AlessioDP/ADP-Core'
                    }
                    ciManagement {
                        system = 'Jenkins'
                        url = 'https://ci.codemc.org/job/AlessioDP/job/ADP%20Core/'
                    }
                }
            }
        }
        repositories {
            mavenLocal()
            if (project.hasProperty('mavenUsername') && project.hasProperty('mavenPassword')) {
                maven {
                    credentials {
                        username project.mavenUsername
                        password project.mavenPassword
                    }

                    def releasesRepoUrl = 'https://repo.codemc.org/repository/maven-releases/'
                    def snapshotsRepoUrl = 'https://repo.codemc.org/repository/maven-snapshots/'
                    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
    }
}