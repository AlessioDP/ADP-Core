apply plugin: 'nu.studer.jooq'

dependencies {
    api group: 'com.google.code.gson', name: 'gson', version: project.ext.gson
    api group: 'commons-lang', name: 'commons-lang', version: project.ext.commons

    testImplementation group: 'com.google.code.gson', name: 'gson', version: project.ext.gson

    api group: 'org.reflections', name: 'reflections', version: project.ext.reflections
    api group: 'net.byteflux', name: 'libby-core', version: project.ext.libby
    api group: 'org.slf4j', name: 'slf4j-api', version: project.ext.slf4j
    api group: 'org.slf4j', name: 'slf4j-simple', version: project.ext.slf4j

    api group: 'org.spongepowered', name: 'configurate-yaml', version: project.ext.configurate
    api group: 'com.zaxxer', name: 'HikariCP', version: project.ext.hikaricp
    api group: 'com.github.AlessioDP', name: 'Simple-YAML', version: project.ext.simpleYaml
    api group: 'org.xerial', name: 'sqlite-jdbc', version: project.ext.sqlitejdbc
    api group: 'com.h2database', name: 'h2', version: project.ext.h2

    api group: 'org.jooq', name: 'jooq', version: project.ext.jooq
    implementation group: 'org.jooq', name: 'jooq-meta', version: project.ext.jooq
    implementation group: 'org.jooq', name: 'jooq-codegen', version: project.ext.jooq

    jooqRuntime(project(":jpa"))
    jooqRuntime("org.jooq:jooq-meta-extensions")
}

javadoc {
    exclude 'com/alessiodp/core/common/jpa'
}

shadowJar {
    archiveClassifier = null

    dependencies {
        include(dependency('com.alessiodp.core.common:.*'))
        include(dependency('com.github.AlessioDP:Simple-YAML'))

        include(dependency('net.byteflux:libby-core'))
    }

    relocate 'org.simpleyaml', 'com.alessiodp.core.common.addons.external.simpleyaml'
    relocate 'net.byteflux.libby', 'com.alessiodp.core.common.addons.external.libby'
}

blossom {
    def lib = 'src/main/java/com/alessiodp/core/common/addons/ADPLibraryManager.java'
    replaceToken '%CONFIGURATE%', project.ext.configurate, lib
    replaceToken '%JOOQ%', project.ext.jooq, lib
    replaceToken '%JOOQ_REACTIVE%', project.ext.jooqReactive, lib
    replaceToken '%HIKARICP%', project.ext.hikaricp, lib
    replaceToken '%REFLECTIONS%', project.ext.reflections, lib
    replaceToken '%SLF4J%', project.ext.slf4j, lib
    replaceToken '%SQLITEJDBC%', project.ext.sqlitejdbc, lib
}

jooq {
    version = project.ext.jooq
    setup(sourceSets.main) {
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.extensions.jpa.JPADatabase'
                includes = '.*'
                excludes = ''
                properties {
                    property {
                        key = 'packages'
                        value = 'com.alessiodp.core.jpa.tables'
                    }
                }
            }
            generate {
            }
            target {
                packageName = 'com.alessiodp.core.common.jpa'
                directory = 'src/generated/java'
            }
        }
    }
}